#!/usr/bin/perl

=head1 NAME

make-scoredict

=head1 SYNOPSIS

        make-scoredict [dictionary-files...]

=head1 DESCRIPTION

Read in lists of words
and emit one line for each pair of anagrams
with the anagram score and two words.

uses the C<Ana> library
which is probably using the MIS algorithm
to calculate scores.

=cut

use Ana;
my %dict;
my %seen;
while (my $w = <>) {
  chomp $w;
  next if $w =~ /[^a-zA-Z]/;
  next if $seen{lc $w}++;
  my $norm = Ana::normalize($w);
  if (exists $dict{$norm}) {
    for my $v ($dict{$norm}->@*) {
      my $score = Ana::score(lc $w, lc $v);
      printf "%02d %s %s\n", $score, sort($w, $v);
    }
  }
  push $dict{$norm}->@*, $w;
} continue {
  print STDERR "." if $. % 10000 == 0;
}

print STDERR "\n";


