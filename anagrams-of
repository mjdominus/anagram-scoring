#!/usr/bin/perl

=head1 NAME

anagrams-of

=head1 SYNOPSIS

        anagrams-of words... [ -- dictionary-files...]
        anagrams-of -f wordlist-file dictionary-files

=head1 DESCRIPTION

Find and print all the anagrams of the specified words
in the given dictionary files.

=cut

my %canon;
use Ana;

if ($ARGV[0] eq "-f") {
  while ($ARGV[0] eq "-f") {
    shift;
    my $file = shift;
    open my($f), "<", $file or die "Couldn't read '$file': $!";
    while (my $word = <$f>) {
      chomp $word;
      my $canon = Ana::normalize($word);
      $canon{$canon} = $word;
    }
  }
} else {
  while (@ARGV) {
    my $word = shift;
    last if $word eq "--";
    my $canon = Ana::normalize($word);
    $canon{$canon} = $word;
  }
}

while (<>) {
  chomp;
  next if $seen{lc $_}++;
  my $norm = Ana::normalize($_);
  if ($canon{$norm} && $canon{$norm} ne $_) {
    print $canon{$norm}, " / ", $_, "\n";
  }
}
